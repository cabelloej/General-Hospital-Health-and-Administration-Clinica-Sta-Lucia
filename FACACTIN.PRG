STORE .T. TO REVERSA
DO WHILE REVERSA
   SELECT ALMTRA
   SET ORDER TO ALMTRA3
   SEEK FACDCGE.NUMERO
   IF .NOT.FOUND()
      EXIT
   ENDIF
   STORE CODIGO   TO WCODIGO
   STORE UNIDADES TO WUNIDADES
   STORE COSTO    TO WCOSTO
   IF RECLOC()
      DELETE
      UNLOCK ALL
   ELSE
      STORE "ACTUALIZACION DE ALMACEN ABORTADA, FAVOR REINTENTAR" TO MES
      DO AVISO WITH MES
      STORE .F. TO REVERSA
      EXIT
   ENDIF
   IF WCODIGO = SPACE(20)
      STORE "ERROR, CODIGO DE ARTICULO VACIO EN TRANS. ALM., VERIFIQUE" TO MES
      DO AVISO WITH MES
      LOOP
   ENDIF
   SELECT ALMART
   SEEK WCODIGO
   IF .NOT.FOUND()
      STORE "CODIGO DE ART:"+RTRIM(WCODIGO)+", EN MOV. NO EXISTE EN ALMACEN, VERIFIQUE" TO MES
      DO AVISO WITH MES
      LOOP
   ENDIF
   IF RECLOC()
      IF UNIEXI + WUNIDADES <> 0
         REPLACE COSEXI WITH ((COSEXI*UNIEXI)+(WCOSTO)) / (UNIEXI+WUNIDADES)
      ELSE
         REPLACE COSEXI WITH 0
      ENDIF
      REPLACE UNIEXI    WITH UNIEXI + WUNIDADES
      FLUSH
      UNLOCK ALL
   ELSE
      STORE "OPERACION ABORTADA EN CALCULO DE COSTO. REINTENTE" TO MES
      DO AVISO WITH MES
   ENDIF
ENDDO
IF .NOT.REVERSA
   RETURN
ENDIF
**** REVERSANDO LOS LOTES AFECTADOS
SELECT ALMLOTTR
GO TOP
LOCATE FOR REFERENCIA = FACDCGE.NUMERO
DO WHILE .NOT. EOF()
   STORE CODART    TO WCODART
   STORE CODLOT    TO WCODLOT
   STORE OPERACION TO WOPERACION
   STORE UNIDADES  TO WUNIX
   IF FILLOC()
      DELETE
      UNLOCK ALL
      FLUSH
   ENDIF
   SELECT ALMLOT
   STORE WCODART+WCODLOT TO WCLA
   SEEK WCLA
   IF FOUND()
      IF RECLOC()
         REPLACE UNIDADES WITH UNIDADES + WUNIX
         FLUSH
         UNLOCK ALL
      ELSE
         STORE "OPERACION ABORTADA EN RECALCULO DE LOTES. REINTENTE" TO MES
         DO AVISO WITH MES
      ENDIF
   ENDIF
   SELECT ALMLOTTR
   CONTINUE
ENDDO

*** GRABA LOS MOVIMIENTOS DE ALMACEN
SELECT FACDCDE
SEEK FACDCGE.NUMERO
DO WHILE .NOT. EOF() .AND. NUMERO = FACDCGE.NUMERO
   IF TIPITEM<>"B"
      SKIP
      LOOP
   ENDIF
   STORE ITEM     TO WITEM
   STORE ITEM     TO WCODIGO
   STORE CANTIDAD TO WCANTIDAD
   STORE PRECIO   TO WPRECIO
   IF WITEM = SPACE(20)
      STORE "ERROR, CODIGO DE ARTICULO VACIO EN RENGLONES. VERIFIQUE" TO MES
      DO AVISO WITH MES
      SKIP
      LOOP
   ENDIF
   SELECT ALMART
   SEEK WITEM
   IF .NOT.FOUND()
      STORE "ERROR, ARTI: "+RTRIM(WITEM)+" DOC.:"+FACDCGE.NUMERO+" NO EXISTE EN ALMACEN" TO MES
      DO AVISO WITH MES
      SELECT FACDCDE
      SKIP
      LOOP
   ENDIF
   STORE (COSEXI*UNIEXI)      TO WCOSTACT
   STORE (WPRECIO*WCANTIDAD)  TO WCOSTFAC
   IF RECLOC()
      REPLACE COSEXI WITH (WCOSTACT-WCOSTFAC)/(UNIEXI-WCANTIDAD)
      REPLACE UNIEXI WITH UNIEXI - WCANTIDAD
      FLUSH
      UNLOCK ALL
   ELSE
      STORE "OPERACION ABORTADA EN RECALCULO DE COSTO, REINTENTE" TO MES
      DO AVISO WITH MES
      SELECT FACDCDE
      SKIP
      LOOP
   ENDIF
   SELECT ALMTRA
   IF FILLOC()
      APPEND BLANK
      REPLACE SERIAL     WITH ALMDATA.SERIAL+1
      REPLACE CODIGO     WITH FACDCDE.ITEM
      REPLACE FECHA      WITH FACDCGE.ELABORADO
      REPLACE OPERACION  WITH "SA"
      REPLACE REFERENCIA WITH FACDCDE.NUMERO
      REPLACE ORIGEN     WITH "FAC"
      REPLACE PROCLI     WITH FACDCGE.DEPTO
      REPLACE UNIDADES   WITH WCANTIDAD
      REPLACE COSTO      WITH WCOSTFAC
      UNLOCK ALL
      FLUSH
      IF ALMART.LOTE = "S"
         SAVE SCRE                TO S9
         STORE FACDCGE.ELABORADO  TO WFECHAOP
         STORE WCANTIDAD          TO WUNIDADES
         STORE "SA"               TO WOPE
         STORE FACDCGE.NUMERO     TO WRESPALDO
         DO ALMTRL
         RESTORE SCRE FROM S9
      ENDIF
      SELECT ALMDATA
      IF RECLOC()
         REPLACE SERIAL WITH SERIAL+1
         FLUSH
         UNLOCK ALL
      ENDIF
   ELSE
      STORE "OPERACION ABORTADA, REPROCESE ALMACEN Y ESTE DOCUMENTO" TO MES
      DO AVISO WITH MES
   ENDIF
   SELECT FACDCDE
   SKIP
ENDDO 
RETURN
