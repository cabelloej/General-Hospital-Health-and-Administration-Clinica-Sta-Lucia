PUSH KEY CLEAR
DEFI WIND WINDGE FROM 1,0 TO 09,79;
          TITLE  " PRESUPUESTOS " ;
          FOOTER " F1=Inc, F2=Mod, F3=Eli, F4=Bus, F5=Precios, F6=Ver, Esc=Salir " ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
DEFI WIND WINDDE FROM 10,0 TO 22,79;
          TITLE " BIENES y SERVICIOS " ;
          FOOTER " F1=Inc, F2=Mod, F3=Eli, F5=Precio, Esc=Salir " ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ON KEY LABEL F1 DO PROGEINC
ON KEY LABEL F2 DO PROGEMOD
ON KEY LABEL F3 DO PROGEELI
ON KEY LABEL F4 DO PROGEBUS
ON KEY LABEL F5 DO PROGEPRE
ON KEY LABEL F6 DO PROGEVER
ACTI WIND WINDGE
SELECT FACPREGE
BROWSE FIELDS NUMERO,NOMTRA1:H="DESCRIPCION DEL PRESUPUESTO",CODCLI:H="CLIENTE";
       NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW WINDGE
DEACT WIND WINDGE
DEACT WIND WINDDE
POP KEY
RETURN
******************
PROCEDURE PROGEINC
******************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGACT
SCATT MEMV BLANK
STORE DATE() TO M.FECHA
DO CREANUM
DO CONVNUM
DO GETGEN
POP KEY
RETURN
******************
PROCEDURE PROGEMOD
******************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGACT
SCATT MEMV
DO GETGEN
POP KEY
RETURN
******************
PROCEDURE PROGEELI
******************
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGACT
STORE 2 TO WOP
DO PROCOPC WITH WFLAGACT
IF WOP=1
   SELECT FACPREGE
   STORE NUMERO TO WNUMDEL
   IF RECLOC()
      DELETE
      UNLOCK
      SELECT FACPREDE
      DO WHILE .T.
         SEEK WNUMDEL
         IF FOUND()
            IF RECLOC()
               DELETE
               UNLOCK
            ELSE
               DO PROCNUL
            ENDIF
         ELSE
            EXIT
         ENDIF
      ENDDO
      UNLOCK ALL
   ELSE
      DO PROCNUL
   ENDIF
ENDIF
POP KEY
RETURN
******************
PROCEDURE PROGEPRE
******************
PUSH KEY CLEAR
STORE "ACTUALIZAR PRECIOS" TO WFLAGACT
STORE 1 TO WOP
DO PROCOPC WITH WFLAGACT
IF WOP=1
   SELECT FACPREDE
   SEEK FACPREGE.NUMERO
   DO WHILE .NOT.EOF().AND.NUMERO=FACPREGE.NUMERO
      IF FACPREDE.TIPITEM="B"
         SELECT ALMART
         SEEK FACPREDE.ITEM
      ELSE
         SELECT SYSSERVI
         SEEK ALLTRIM(FACPREDE.ITEM)
      ENDIF
      IF FOUND()
         STORE  PRECIO TO WPRECIO
      ELSE
         STORE "ITEM: "+FACPREDE.ITEM+" NO REGISTRADO" TO WTEXT
         DO AVISO WITH WTEXT
         STORE 0       TO WPRECIO
      ENDIF
      SELECT FACPREDE
      IF WPRECIO>0
         IF RECLOC()
            REPLACE PRECIO WITH WPRECIO
            UNLOCK
         ELSE
            DO PROCNUL
         ENDIF
      ENDIF
      SELECT FACPREDE
      SKIP
   ENDDO
ENDIF
POP KEY
RETURN
******************
PROCEDURE PROGEBUS
******************
PUSH KEY CLEAR
DEFI WIND WINDBUS FROM 12,12 TO 14,55;
          TITLE " BUSCAR ";
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTIVATE WIND WINDBUS
STORE 0 TO M.NUMERO
@ 00,03 SAY "NUMERO: " GET M.NUMERO PICTURE "9999999"
READ
DO CONVNUM
SEEK M.NUMERO
RELE WIND WINDBUS
POP KEY
RETURN
******************
PROCEDURE PROGEVER
******************
PUSH KEY CLEAR
DEFI WIND WINDVER FROM 12,19 TO 14,59;
          TITLE " SALIDA ";
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTIVATE WIND WINDVER
DO WHILE .T.
   STORE 1 TO WOP
   @ 00,03 SAY "OPCIONES: ";
   GET WOP PICT "@*H MONITOR  ;IMPRESORA "
   READ
   IF LASTKEY()=13
      EXIT
   ENDIF
ENDDO
RELE WIND WINDVER
STORE FACPREGE.NUMERO TO WNUMERO
STORE WOP             TO WSALIDA
DO FACIMPPR
POP KEY
RETURN
******************************* RUTINAS ***********************************
************
PROC CREANUM
************
SELECT FACDATA
IF RECLOC()
   REPLACE FACSERIPRE WITH FACSERIPRE+1
   STORE FACSERIPRE   TO   M.NUMERO
   FLUSH
   UNLOCK ALL
ENDIF
RETURN
************
PROC CONVNUM
************
STORE LTRIM(STR(M.NUMERO,7))                  TO M.NUMERO
STORE REPLICATE("0",7-LEN(M.NUMERO))+M.NUMERO TO M.NUMERO
RETURN
***********
PROC GETGEN
***********
PUSH KEY CLEAR
DEFI WIND WINDGETG FROM 2,0 TO 09,79;
          TITLE  " "+WFLAGACT+" " ;
          FOOTER " Esc=Salir";
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDGETG
STORE M.REFERE TO WLASTREF
@ 0,1 SAY "Numero     :" +   M.NUMERO
@ 1,1 SAY "Fecha      :" GET M.FECHA
@ 2,1 SAY "Descripcion:" GET M.NOMTRA1
@ 3,1 SAY "           :" GET M.NOMTRA2
IF WFLAGACT="INCLUIR"
   @ 4,1 SAY "Rfcia.Elab.:" GET M.REFERE VALID VALREF()
ENDIF
@ 5,1 SAY "Cliente    :" GET M.CODCLI VALID VALCLI()
READ
STORE 1 TO WOP
DO PROCOPC WITH WFLAGACT
IF WOP=1
   SELECT FACPREGE
   IF WFLAGACT="INCLUIR"
      IF FILLOC()
         APPEND BLANK
         GATH MEMV
         UNLOCK ALL
      ELSE
        DO PROCNUL
      ENDIF
   ELSE
      IF RECLOC()
         GATH MEMV
         UNLOCK ALL
      ELSE
        DO PROCNUL
      ENDIF
   ENDIF
ENDIF
*** RENGLONES
ACTI WIND WINDDE
ON KEY LABEL F1 DO PRORENI
ON KEY LABEL F2 DO PRORENM
ON KEY LABEL F3 DO PRORENE
ON KEY LABEL F5 DO PRORENP
SELECT FACPREDE
BROWSE FIELDS WITEMDES=FUNCITEM():H="Descripcion",CANTIDAD:P="99,999.99",PRECIO:P="99,999,999.99",DESCUENTO:P="999.99":H="Dscto.",;
              TOTAL=CANTIDAD*(PRECIO-(PRECIO*DESCUENTO/100)):H="Total renglon":P="99,999,999.99";
       NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW WINDDE;
       KEY FACPREGE.NUMERO
DEACT WIND WINDGETG
DEACT WIND WINDDE
POP KEY
RETURN
************
PROC PRORENI
************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGR
SELECT FACPREDE
SCATT MEMV BLANK
STORE FACPREGE.NUMERO TO M.NUMERO
DO GETREN
POP KEY
RETURN
************
PROC PRORENM
************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGR
SELECT FACPREDE
SCATT MEMV
DO GETREN
POP KEY
RETURN
************
PROC PRORENE
************
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGR
STORE 2 TO WOP
DO PROCOPC WITH WFLAGR
IF WOP=1
   SELECT FACPREDE
   IF RECLOC()
      DELETE
      UNLOCK
   ELSE
      DO PROCNUL
   ENDIF
ENDIF
POP KEY
RETURN
************
PROC PRORENP
************
PUSH KEY CLEAR
STORE "ACT. PRECIO RENGLON" TO WFLAGACT
STORE 1 TO WOP
DO PROCOPC WITH WFLAGACT
IF WOP=1
   SELECT FACPREDE
   IF FACPREDE.TIPITEM="B"
      SELECT ALMART
      SEEK FACPREDE.ITEM
   ELSE
      SELECT SYSSERVI
      SEEK ALLTRIM(FACPREDE.ITEM)
   ENDIF
   IF FOUND()
      STORE  PRECIO TO WPRECIO
   ELSE
      STORE "ITEM: "+FACPREDE.ITEM+" NO REGISTRADO" TO WTEXT
      DO AVISO WITH WTEXT
      STORE 0       TO WPRECIO
   ENDIF
   SELECT FACPREDE
   IF WPRECIO>0
      IF RECLOC()
         REPLACE PRECIO WITH WPRECIO
         UNLOCK
      ELSE
         DO PROCNUL
      ENDIF
   ENDIF
ENDIF
POP KEY
RETURN
***********
PROC GETREN
***********
DEFI WIND WINDGETR FROM 11,0 TO 22,79;
          TITLE  WFLAGR ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDGETR
IF WFLAGR="INCLUIR"
   @ 0,1 SAY "Tipo (B/S) :" GET M.TIPITEM    VALID VALTIP()
   @ 1,1 SAY "Codigo     :" GET M.ITEM       VALID VALITE()
   @ 2,1 SAY "Descripcion:"
ELSE
   @ 0,1 SAY "Tipo (B/S) :" +   M.TIPITEM
   @ 1,1 SAY "Codigo     :" +   M.ITEM       
   @ 2,1 SAY "Descripcion:"
   DO VALITE
ENDIF
@ 3,1 SAY "Cantidad   :" GET M.CANTIDAD   PICTURE "999,999.99"
@ 4,1 SAY "Precio     :" GET M.PRECIO     PICTURE "99,999,999.99"
@ 5,1 SAY "Descuento  :" GET M.DESCUENTO  PICTURE "999.99"
@ 6,1 SAY "Total      :"
READ
STORE M.CANTIDAD*(M.PRECIO-(M.PRECIO*M.DESCUENTO/100)) TO WTOTREN
@ 6,14 SAY WTOTREN PICTURE "99,999,999.99"
STORE 1 TO WOP
DO PROCOPC WITH WFLAGR
IF WOP=1
   SELECT FACPREDE
   IF WFLAGR="INCLUIR"
      IF FILLOC()
         APPEND BLANK
         GATH MEMV
         UNLOCK ALL
      ELSE
        DO PROCNUL
      ENDIF
   ELSE
      IF RECLOC()
         GATH MEMV
         UNLOCK ALL
      ELSE
        DO PROCNUL
      ENDIF
   ENDIF
ENDIF
RELE WIND WINDGETR
RETURN
*************
FUNC FUNCITEM
*************
IF FACPREDE.TIPITEM="B"
   SELECT ALMART
   SEEK FACPREDE.ITEM
   IF FOUND()
      STORE  DESCRI                  TO WDESITEM
   ELSE
      STORE  "NO REG. EN ARTICULOS"  TO WDESITEM
   ENDIF
ELSE
   IF FACPREDE.TIPITEM="S"
      SELECT SYSSERVI
      SEEK ALLTRIM(FACPREDE.ITEM)
      IF FOUND()
         STORE  DESCRI                 TO WDESITEM
      ELSE
         STORE  "NO REG. EN SERVICIOS" TO WDESITEM
      ENDIF
   ELSE
      STORE FACPREDE.ITEM              TO WDESITEM
   ENDIF
ENDIF
SELECT FACPREDE
RETURN SUBSTR(WDESITEM,1,30)
***********
FUNC VALTIP
***********
IF M.TIPITEM="B".OR.M.TIPITEM="S"
   RETURN .T.
ELSE
   RETURN .F.
ENDIF
***********
FUNC VALITE
***********
IF M.TIPITEM="B"
   SELECT ALMART
   SEEK M.ITEM
   IF .NOT.FOUND()
      PUSH KEY CLEAR
      DO MEDCONVA
      POP KEY
   ENDIF
   @ 2,14 SAY DESCRI
   IF WFLAGR="INCLUIR"
      STORE CODIGO  TO M.ITEM
      STORE PRECIO  TO M.PRECIO
   ENDIF
   SELECT FACPREDE
   RETURN .T.
ELSE
   DO WHILE .T.
      SELECT SYSSERVI
      SEEK ALLTRIM(M.ITEM)
      IF .NOT.FOUND().OR.LEN(ALLTRIM(M.ITEM))=0
         PUSH KEY CLEAR
         STORE 05 TO LIBRSER
         STORE 00 TO CIBRSER
         DO SERWINSE
         POP KEY
         IF LEN(ALLTRIM(SERVICIO))<>12
            STORE SPACE(12) TO M.ITEM
            LOOP
         ELSE
            STORE SERVICIO TO M.ITEM
         ENDIF
      ELSE
         @ 2,14 SAY DESCRI
         IF WFLAGR="INCLUIR"
            STORE SERVICIO TO M.ITEM
            STORE PRECIO   TO M.PRECIO
         ENDIF
         EXIT
      ENDIF
   ENDDO
   SELECT FACPREDE
   RETURN .T.
ENDIF
RETURN
***********
FUNC VALREF
***********
IF WLASTREF=M.REFERE.OR.M.REFERE=SPACE(10)
   RETURN .T.
ELSE
   STORE M.NUMERO TO WNEW
   STORE M.REFERE TO WREF
   STORE RECNO()  TO WRECORI
   SELECT FACPREGE
   SEEK WREF
   IF .NOT.FOUND()
      STORE "REFERENCIA NO EXISTE COMO PRESUPUESTO, VERIFIQUE" TO WTEXT
      DO AVISO WITH WTEXT
      GO WRECORI
      RETURN .F.
   ENDIF
   SELECT FACPREDE
   DO WHILE .T.
      SEEK WNEW
      IF FOUND()
         IF RECLOC()
            DELETE
            UNLOCK
         ELSE
            DO PROCNUL
         ENDIF
      ELSE
         EXIT
      ENDIF
   ENDDO
   UNLOCK ALL
   SEEK WREF
   DO WHILE .NOT.EOF().AND.NUMERO=WREF
      STORE RECNO() TO WLASTREC
      SCAT MEMV
      STORE WNEW TO M.NUMERO
      IF FILLOC()
         APPEND BLANK
         GATH MEMV
         UNLOCK ALL
      ENDIF
      GO WLASTREC
      SKIP
   ENDDO
   GO WRECORI
   SELECT FACPREGE
ENDIF
RETURN
***********
FUNC VALCLI
***********
STORE .T. TO VIENDOCLI
DO WHILE VIENDOCLI
   IF M.CODCLI=SPACE(15)
      EXIT
   ENDIF
   IF M.CODCLI="*"+SPACE(14)
      STORE .F. TO VIENDOCLI
      EXIT
   ENDIF
   SELECT FACCLI
   SEEK M.CODCLI
   IF FOUND()
      STORE DESCLI TO M.NOMCLI
      @ 5,30 SAY M.NOMCLI
      RETURN .T.
   ELSE
      DEFI WIND WINDCL FROM 10,0 TO 22,79;
           TITLE " CLIENTES " ;
           FOOTER " Esc=Seleccionar " ;
           DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
      ACTI WIND WINDCL
      BROWSE FIELDS CODCLI:H="CODIGO",DESCLI:H="CLIENTE";
      NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW WINDCL
      DEACT WIND WINDCL
      STORE CODCLI TO M.CODCLI
   ENDIF
ENDDO
IF .NOT. VIENDOCLI
   STORE "*"+SPACE(14) TO M.CODCLI
   @ 5,30 GET M.NOMCLI
   READ
ENDIF
RETURN .T.
************
PROC PROCOPC
************
PARA WTITLE
DEFI WIND WINDCON FROM 12,19 TO 14,59;
          TITLE WTITLE;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTIVATE WIND WINDCON
DO WHILE .T.
   @ 00,03 SAY "OPCIONES: ";
   GET WOP PICT "@*H ACEPTAR  ;CANCELAR "
   READ
   IF LASTKEY()=13
      EXIT
   ENDIF
ENDDO
RELE WIND WINDCON
RETURN
************
PROC PROCNUL
************
STORE "OPERACION RECHAZADA, FAVOR REINTENTAR." TO WTEXT
DO AVISO WITH WTEXT
RETURN
************
