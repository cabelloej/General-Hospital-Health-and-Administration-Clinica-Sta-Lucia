   *** VALORES INICIALES DE CONFIG. DE DATOS

   STORE SPACE(2)  TO M.CODSUB
   STORE SPACE(3)  TO M.CODREN
   STORE SPACE(4)  TO M.CODTAB
   STORE SPACE(11) TO M.CODDAT
   STORE SPACE(1)  TO WTITLE
   STORE SPACE(1)  TO WESTACT
   STORE 0         TO WTOTLIN
   STORE 0         TO WTOTCOL
   STORE SPACE(1)  TO WTIPVAL
   STORE SPACE(1)  TO WDESDEVAL
   STORE SPACE(1)  TO WHASTAVAL
   STORE SPACE(1)  TO WCHR

   *** SUBGRUPOS DE LA ACTIVIDAD
   STORE 6        TO LIBRSUB,LIWINBR
   STORE 5        TO CIBRSUB
   STORE 1        TO LIWINDAT
   STORE 1        TO LILASSUB
   STORE 12       TO WWINSIZE
   SELECT HISSUB
   GO TOP
   DEFI WIND BRHD  FROM LIBRSUB,CIBRSUB TO LIBRSUB+WWINSIZE,CIBRSUB+70 ;
                   TITLE DTOC(M.FECACT)+" "+RTRIM(HISGRU.DESGRU)+"  Dr(a)."+RTRIM(SYSMED.NOMBRE);
                   FOOTER " Enter=Actualizar, F9=Marcar/Desmarcar, ESC=Salir ";
                   DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
   ACTI WIND BRHD
   ON KEY LABEL ENTER DO ACTHIS
   ON KEY LABEL F9 DO GRABAMAR
   BROWSE FIELDS DESSUB:H="DESCRIPCION",DATA=GETCHRS():H="REGISTROS           ",UNDSUB:H="UNIDAD";
                 KEY HISGRU.CODGRU;
                 NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW BRHD WHEN VALORESS()
   RELE WIND BRHD
   ON KEY LABEL ENTER
   ON KEY LABEL F9
   RETURN
                 *FOR CODGRU=HISGRU.CODGRU;
   *******************************************************
   ************
   PROC ACTHIS
   ************
   PUSH KEY CLEAR
   CLEAR TYPEAHEAD
   STORE WESTACT      TO WLASTEST
   STORE LIWINDAT     TO WLILASSUB
   IF ESTSUB="L"
      DO HISGETDA
   ELSE
      *** RENGLONES DE LA SUBACTIVIDAD
      STORE 7         TO LIBRREN,LIWINBR
      STORE 7         TO CIBRREN
      STORE 1         TO LIWINDAT
      STORE 1         TO LILASREN
      STORE 12        TO WWINSIZE
      SELECT HISREN
      GO TOP
      DEFI WIND BRHDR FROM LIBRREN,CIBRREN TO LIBRREN+WWINSIZE,CIBRREN+70 ;
                      TITLE " "+RTRIM(HISSUB.DESSUB)+" ";
                      FOOTER " Enter=Actualizar, F9=Marcar/Desmarcar, ESC=Salir ";
                      DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
      ACTI WIND BRHDR
      ON KEY LABEL ENTER DO ACTHIS2
      ON KEY LABEL F9 DO GRABAMAR
      BROWSE FIELDS DESREN:H="DESCRIPCION", DATA=GETCHRR():H="REGISTROS           ",UNDREN:H="UNIDAD" ;
                    KEY HISGRU.CODGRU+HISSUB.CODSUB;
                    NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW BRHDR WHEN VALORESR()
      RELE WIND BRHDR
   ENDIF
   STORE WLASTEST TO WESTACT
   STORE LILASSUB TO LIWINDAT
   POP KEY
   RETURN
                    *FOR CODGRU=HISGRU.CODGRU.AND.CODSUB=HISSUB.CODSUB;
   *************
   PROC ACTHIS2
   *************
   PUSH KEY CLEAR
   CLEAR TYPEAHEAD
   STORE WESTACT      TO WLASTEST
   STORE LIWINDAT     TO LILASREN
   IF ESTREN="L"
      DO HISGETDA
   ELSE
      STORE 8         TO LIBRTAB,LIWINBR
      STORE 9         TO CIBRTAB
      STORE 1         TO LIWINDAT
      STORE 1         TO LILASTAB
      STORE 12        TO WWINSIZE
      SELECT HISTAB
      GO TOP
      DEFI WIND BRTAB FROM LIBRTAB,CIBRTAB TO LIBRTAB+WWINSIZE,CIBRTAB+70 ;
                      TITLE " "+RTRIM(HISREN.DESREN)+" ";
                      FOOTER " Enter=Actualizar, F9=Marcar/Desmacar, ESC=Salir ";
                      DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
      ACTI WIND BRTAB
      ON KEY LABEL ENTER DO HISGETDA
      ON KEY LABEL F9 DO GRABAMAR
      BROWSE FIELDS DESTAB:H="DESCRIPCION", DATA=GETCHRT():H="REGISTROS           ", UNDTAB:H="UNIDAD" ;
             KEY HISGRU.CODGRU+HISSUB.CODSUB+HISREN.CODREN;
             NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW BRTAB WHEN VALOREST()
      RELE WIND BRTAB
   ENDIF
   STORE WLASTEST TO WESTACT
   STORE LILASREN TO LIWINDAT
   POP KEY
   RETURN
             *FOR CODGRU=HISGRU.CODGRU.AND.CODSUB=HISSUB.CODSUB.AND.CODREN=HISREN.CODREN;
   *****************************************************************************
   *****************************************************************************

   **********************
   PROC HISGETDA
   **********************
   PUSH KEY CLEAR
   CLEAR TYPEAHEAD
   IF WTOTLIN=0.OR.WTOTCOL=0
      STORE 15  TO WTOTLIN
      STORE 70  TO WTOTCOL
   ENDIF
   DIME WHISDAT(100)
   *DIME WHISDAT(WTOTLIN+1)
   *** PONE LA TABLA EN BLANCO
   STORE 1 TO WCONTLIN
   DO WHILE WCONTLIN<=WTOTLIN
      STORE SPACE(WTOTCOL) TO WHISDAT(WCONTLIN)
      STORE WCONTLIN+1 TO WCONTLIN
   ENDDO
   *** CARGA LOS POSIBLES DATOS QUE EXISTAN
   SELECT HISDET
   STORE WCODPAC+M.CODACT+STR(M.NUMACT,4)+M.CODDAT TO WCLAVEDET
   SEEK WCLAVEDET
   STORE 0 TO WCONTLIN
   DO WHILE .NOT. EOF().AND.(CODPAC+CODACT+STR(NUMACT,4)+CODDAT)=WCLAVEDET
      STORE WCONTLIN+1 TO WCONTLIN
      STORE SUBSTR(DATREN,1,WTOTCOL) TO WHISDAT(WCONTLIN)
      SKIP
   ENDDO
   *DO WHILE .NOT. EOF().AND.(WCODPAC+M.CODACT+STR(M.NUMACT,4)+CODDAT)=WCLAVEDET
   *   STORE WCONTLIN+1 TO WCONTLIN
   *   STORE SUBSTR(DATREN,1,WTOTCOL) TO WHISDAT(WCONTLIN)
   *   SKIP
   *ENDDO
   *** DESPLIEGA LA VENTANA DE EDICION
   *IF WTOTLIN=1.AND.WTOTCOL<=20
   IF WTOTLIN=1.AND.WTOTCOL<=0
      STORE LIWINBR+LIWINDAT+2      TO WINILIN
      STORE 42                      TO WINICOL
      DEFINE WIND WINDAT FROM WINILIN,WINICOL TO (WINILIN+WTOTLIN)+1, WINICOL+WTOTCOL+1;
             NONE NOFLOAT NOZOOM NOGROW NOSHADOW COLOR SCHEME 10
   ELSE
      STORE INT(((80-WTOTCOL)/2)-1) TO WINICOL
      STORE LIWINBR+1               TO WINILIN
      DEFINE WIND WINDAT FROM WINILIN,WINICOL TO (WINILIN+WTOTLIN)+1,WINICOL+WTOTCOL+1;
             TITLE " "+ALLTRIM(WTITLE)+" ";
             FOOTER " ESC = Salir,  * = Marcar ";
             DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
   ENDIF
   ACTI WIND WINDAT
   *** MUESTRA VALORES ACTUALES
   STORE 1 TO WCONTLIN
   DO WHILE WCONTLIN<=WTOTLIN
      @ WCONTLIN-1,0 SAY WHISDAT(WCONTLIN)
      STORE WCONTLIN+1 TO WCONTLIN
   ENDDO
   *** EDITA LOS VALORES
   IF CODDOC=WCODMED.OR..NOT.WFLAGMED.OR.HISGRU.PROGRU="M"
      STORE 1 TO WCONTLIN
      IF WTIPVAL="N"
         DO WHILE WCONTLIN<=WTOTLIN
            STORE REPLICATE("9",WTOTCOL-3)+".99" TO WMASCARA
            STORE VAL(WHISDAT(WCONTLIN)) TO WHISDAT(WCONTLIN)
            @ WCONTLIN-1,0 GET WHISDAT(WCONTLIN) PICTURE WMASCARA VALID VALDAT()
            READ
            STORE LTRIM(STR(WHISDAT(WCONTLIN),(WTOTCOL-3),2)) TO WHISDAT(WCONTLIN)
            IF LASTKEY()=27
               EXIT
            ENDIF
            STORE WCONTLIN+1 TO WCONTLIN
         ENDDO
      ELSE
         IF WTIPVAL="C"
            DO WHILE WCONTLIN<=WTOTLIN
               @ WCONTLIN-1,0 GET WHISDAT(WCONTLIN) VALID VALDAT()
               READ
               IF LASTKEY()=27
                  EXIT
               ENDIF
               STORE WCONTLIN+1 TO WCONTLIN
            ENDDO
         ELSE
            DO WHILE WCONTLIN<=WTOTLIN
               @ WCONTLIN-1,0 GET WHISDAT(WCONTLIN)
               STORE WCONTLIN+1 TO WCONTLIN
            ENDDO
            READ
         ENDIF
      ENDIF
   ELSE
      STORE "OPRIMA <ENTER> PARA CONTINUAR" TO WTEXT
      DO AVISO WITH WTEXT
      RELE WIND WINDAT
      POP KEY
      RETURN
   ENDIF
   *** GRABA Y CIERRA VENTANA
   DO GRABANOR
   RELE WIND WINDAT
   POP KEY
   RETURN
   ******************
   PROCEDURE GRABANOR
   ******************
   PUSH KEY CLEAR
   *** BORRA LO POSIBLE VIEJO Y GRABA LO NUEVO
   SELECT HISDET
   IF FILLOC()
      STORE WCODPAC+M.CODACT+STR(M.NUMACT,4)+M.CODDAT TO WCLAVEDET
      DO WHILE .T.
         SEEK WCLAVEDET
         IF FOUND()
            DELETE
         ELSE
            EXIT
         ENDIF
      ENDDO
      STORE 1 TO WCONTLIN
      DO WHILE WCONTLIN<=WTOTLIN
         IF WHISDAT(WCONTLIN)<>SPACE(WTOTCOL)
            SELECT HISDET
            APPEND BLANK
            REPLACE CODPAC WITH WCODPAC
            REPLACE CODACT WITH M.CODACT
            REPLACE NUMACT WITH M.NUMACT
            REPLACE CODDAT WITH M.CODDAT
            REPLACE DATREN WITH WHISDAT(WCONTLIN)
         ENDIF
         STORE WCONTLIN+1 TO WCONTLIN
      ENDDO
      UNLOCK ALL
   ELSE
      STORE "OPERACION CANCELADA, REINTENTE" TO WTEXT
      DO AVISO WITH WTEXT
   ENDIF
   POP KEY
   RETURN
   ******************
   PROCEDURE GRABAMAR
   ******************
   PUSH KEY CLEAR
   IF WESTACT="T"
      STORE "NO SE PUEDE MARCAR SI EXISTEN DIVISIONES, ACTUALICE CON [ENTER]" TO WTEXT
      DO AVISO WITH WTEXT
      POP KEY
      RETURN
   ENDIF
   *** BORRA LO POSIBLE VIEJO Y COLOCA ASTERISCO EN DATOS PARA MARCAR
   STORE .F. TO WFLAGMAR
   SELECT HISDET
   IF FILLOC()
      STORE WCODPAC+M.CODACT+STR(M.NUMACT,4)+M.CODDAT TO WCLAVEDET
      DO WHILE .T.
         SEEK WCLAVEDET
         IF FOUND()
            DELETE
            STORE .T. TO WFLAGMAR
         ELSE
            EXIT
         ENDIF
      ENDDO
      IF .NOT.WFLAGMAR
         SELECT HISDET
         APPEND BLANK
         REPLACE CODPAC WITH WCODPAC
         REPLACE CODACT WITH M.CODACT
         REPLACE NUMACT WITH M.NUMACT
         REPLACE CODDAT WITH M.CODDAT
         REPLACE DATREN WITH "*"
      ENDIF
      UNLOCK ALL
   ELSE
      STORE "OPERACION CANCELADA, REINTENTE" TO WTEXT
      DO AVISO WITH WTEXT
   ENDIF
   POP KEY
   RETURN
   *************
   FUNC VALORESS
   *************
   PUSH KEY CLEAR
   STORE CODSUB                              TO M.CODSUB
   STORE DESSUB                              TO WTITLE
   STORE ESTSUB                              TO WESTACT
   STORE LONSUB                              TO WTOTLIN
   STORE ANCSUB                              TO WTOTCOL
   STORE TIPVAL                              TO WTIPVAL
   STORE VL1SUB                              TO WDESDEVAL
   STORE VL2SUB                              TO WHASTAVAL
   STORE M.CODACT+M.CODSUB+SPACE(3)+SPACE(3) TO M.CODDAT
   DO LINBAR
   POP KEY
   RETURN
   *************
   FUNC VALORESR
   *************
   PUSH KEY CLEAR
   STORE CODREN                              TO M.CODREN
   STORE DESREN                              TO WTITLE
   STORE ESTREN                              TO WESTACT
   STORE LONREN                              TO WTOTLIN
   STORE ANCREN                              TO WTOTCOL
   STORE TIPVAL                              TO WTIPVAL
   STORE VL1REN                              TO WDESDEVAL
   STORE VL2REN                              TO WHASTAVAL
   STORE M.CODACT+M.CODSUB+M.CODREN+SPACE(3) TO M.CODDAT
   DO LINBAR
   POP KEY
   RETURN
   *************
   FUNC VALOREST
   *************
   PUSH KEY CLEAR
   STORE CODTAB                              TO M.CODTAB
   STORE DESTAB                              TO WTITLE
   STORE "L"                                 TO WESTACT
   STORE LONTAB                              TO WTOTLIN
   STORE ANCTAB                              TO WTOTCOL
   STORE TIPVAL                              TO WTIPVAL
   STORE VL1TAB                              TO WDESDEVAL
   STORE VL2TAB                              TO WHASTAVAL
   STORE M.CODACT+M.CODSUB+M.CODREN+M.CODTAB TO M.CODDAT
   DO LINBAR
   POP KEY
   RETURN
   ****************
   FUNC GETCHRS
   ****************
   PUSH KEY CLEAR
   SELECT HISDET
   STORE HISGEN.CODPAC+HISGEN.CODACT+STR(HISGEN.NUMACT,4)+;
         HISSUB.CODGRU+HISSUB.CODSUB                           TO WCLAVECHR
   SEEK WCLAVECHR
   IF FOUND()
      STORE "Oprima [Enter]"                                   TO WCHR
      STORE HISGEN.CODPAC+HISGEN.CODACT+STR(HISGEN.NUMACT,4)+;
            HISSUB.CODGRU+HISSUB.CODSUB+SPACE(6)               TO WCLAVECHR
      SEEK WCLAVECHR
      IF FOUND()
         IF SUBSTR(DATREN,1,1)="*"
            STORE CHR(251)                                     TO WCHR
         ELSE
            STORE SUBSTR(DATREN,1,20)                          TO WCHR
         ENDIF
      ENDIF
   ELSE
      STORE " "                                                TO WCHR
   ENDIF
   SELECT HISSUB
   POP KEY
   RETURN WCHR
   ****************
   FUNC GETCHRR
   ****************
   PUSH KEY CLEAR
   SELECT HISDET
   STORE HISGEN.CODPAC+HISGEN.CODACT+STR(HISGEN.NUMACT,4)+;
         HISREN.CODGRU+HISREN.CODSUB+HISREN.CODREN             TO WCLAVECHR
   SEEK WCLAVECHR
   IF FOUND()
      STORE "Oprima [Enter]"                                   TO WCHR
      STORE HISGEN.CODPAC+HISGEN.CODACT+STR(HISGEN.NUMACT,4)+;
            HISREN.CODGRU+HISREN.CODSUB+HISREN.CODREN+SPACE(3) TO WCLAVECHR
      SEEK WCLAVECHR
      IF FOUND()
         IF SUBSTR(DATREN,1,1)="*"
            STORE CHR(251)                                     TO WCHR
         ELSE
            STORE SUBSTR(DATREN,1,20)                          TO WCHR
         ENDIF
      ENDIF
   ELSE
      STORE " "                                                TO WCHR
   ENDIF
   SELECT HISREN
   POP KEY
   RETURN WCHR
   ****************
   FUNC GETCHRT
   ****************
   PUSH KEY CLEAR
   SELECT HISDET
   STORE HISGEN.CODPAC+HISGEN.CODACT+STR(HISGEN.NUMACT,4)+;
         HISTAB.CODGRU+HISTAB.CODSUB+HISTAB.CODREN+HISTAB.CODTAB TO WCLAVECHR
   SEEK WCLAVECHR
   IF FOUND()
      IF SUBSTR(DATREN,1,1)="*"
         STORE CHR(251)                                   TO WCHR
      ELSE
         STORE SUBSTR(DATREN,1,20)                        TO WCHR
      ENDIF
   ELSE
      STORE " " TO WCHR
   ENDIF
   SELECT HISTAB
   POP KEY
   RETURN WCHR
   ***********
   FUNC VALDAT
   ***********
   IF WDESDEVAL <> SPACE(WTOTCOL)
      IF WTIPVAL="N" 
         IF VAL(WDESDEVAL) > WHISDAT(WCONTLIN)
            STORE "EL VALOR INGRESADO ES INFERIOR A "+ALLTRIM(WDESDEVAL) TO WTEXT
            DO AVISO WITH WTEXT
            RETURN .F.
         ENDIF
      ELSE
         IF WTIPVAL="C"
            *** CREAR VARIABLE TEMPORAL RELLENADA CON ESPACION A LA IZQ.
            *** CON EL DATO INGRESADO HASTA IGUALAR LA LONGITUD DEL VALIDADOR
            IF ALLTRIM(WDESDEVAL) > ALLTRIM(WHISDAT(WCONTLIN))
               STORE "EL VALOR INGRESADO ES INFERIOR A "+ALLTRIM(WDESDEVAL) TO WTEXT
               DO AVISO WITH WTEXT
               RETURN .F.
            ENDIF
         ELSE
           *STORE "VALIDADOR (DESDE) NO DEFINIDO" TO WTEXT
           *DO AVISO WITH WTEXT
         ENDIF
      ENDIF
   ENDIF
   IF WHASTAVAL <> SPACE(WTOTCOL)
      IF WTIPVAL="N" 
         IF VAL(WHASTAVAL) < WHISDAT(WCONTLIN)
            STORE "EL VALOR INGRESADO ES SUPERIOR A "+ALLTRIM(WHASTAVAL) TO WTEXT
            DO AVISO WITH WTEXT
            RETURN .F.
         ENDIF
      ELSE
         IF WTIPVAL="C"
            *** CREAR VARIABLE TEMPORAL RELLENA CON ESPACION A LA IZQ.
            *** CON EL DATO INGRESADO HASTA IGUALAR LA LONGITUD DEL VALIDADOR
            IF ALLTRIM(WHASTAVAL) > ALLTRIM(WHISDAT(WCONTLIN))
               STORE "EL VALOR INGRESADO ES SUPERIOR A "+ALLTRIM(WHASTAVAL) TO WTEXT
               DO AVISO WITH WTEXT
               RETURN .F.
            ENDIF
         ELSE
           *STORE "VALIDADOR (HASTA) NO DEFINIDO" TO WTEXT
           *DO AVISO WITH WTEXT
         ENDIF
      ENDIF
   ENDIF
   RETURN .T.
   **************
   PROC LINBAR
   **************
   *** UBICACION DE LA BARRA
   IF LASTKEY()=5
      *** UP
      STORE LIWINDAT-1 TO LIWINDAT
      IF LIWINDAT<=0
         STORE 1 TO LIWINDAT
      ENDIF
   ELSE
      IF LASTKEY()=24
         *** DOWN
         STORE LIWINDAT+1 TO LIWINDAT
         IF LIWINDAT>(WWINSIZE-3)
            STORE (WWINSIZE-3) TO LIWINDAT
         ENDIF
      ELSE
         IF LASTKEY()=18
            *** PAGE UP
            STORE 1 TO LIWINDAT
            IF LIWINDAT<=0
               STORE 1 TO LIWINDAT
            ENDIF
         ELSE
            IF LASTKEY()=3
               *** PAGE DOWN
               STORE (WWINSIZE-3) TO LIWINDAT
               IF LIWINDAT>(WWINSIZE-3)
                  STORE WWINSIZE-3 TO LIWINDAT
               ENDIF
            ENDIF
         ENDIF
      ENDIF
   ENDIF
   ***
   RETURN
   ********

